# 1. SICP(마법사책) 공부

# 1.1. Exercise 2.33

accumulate 함수를 이용해서 map, append, length 함수를 다시 작성해보기

```scheme
;rewrite map
(define (my-map p sequence)
  (accumulate (lambda (x y) (cons (p x) y)) nil sequence))

(display (my-map square (list 1 2 3 4))) (newline)

;rewrite append
(define (my-append seq1 seq2)
  (accumulate cons seq2 seq1))

(display (my-append (list 1 2 3) (list 4 5 6))) (newline)

;rewrite length
(define (my-length sequence)
  (accumulate (lambda (x y) (+ 1 y)) 0 sequence))

(display (my-length (list 1 2 3 4 5 6 7 8 9))) (newline)
```

실행결과

```
(1 4 9 16)
(1 2 3 4 5 6)
9

```

## 1.2. Exercise 2.35

tree에서 leaf 개수를 세는 함수 count-leaves를 accumulate를 사용하여 다시 작성해보기.

```scheme
;from Chapter 2.3.2
(define (enumerate-tree tree)
  (cond ((null? tree) nil)
        ((not (pair? tree)) (list tree))
        (else (append (enumerate-tree (car tree))
                      (enumerate-tree (cdr tree))))))

(display (enumerate-tree (list 1 (list 2 (list 3 4)) 5))) (newline)

;rewrite count-leaves
(define (count-leaves t)
  (accumulate + 0 (map (lambda (x) 1) (enumerate-tree t))))

(display (count-leaves (list 1 (list 2 (list 3 4) 5) (list 6 7)))) (newline)

```

실행결과

```
(1 2 3 4 5)
7

```
