# 1. Scheme(Guile)에서 계산에 걸리는 시간 측정하기

## 1.1. 예시 알고리즘 : 소수인지 확인하는 함수 prime?

```scheme
(define (smallest-divisor n)
  (find-divisor n 2))
(define (find-divisor n test-divisor)
  (cond ((> (square test-divisor) n) n)
        ((divides? test-divisor n) test-divisor)
        (else (find-divisor n (+ test-divisor 1)))))
(define (divides? a b)
  (= (remainder b a) 0))
(define (prime? n)
  (= n (smallest-divisor n)))

(display (prime? 2)) (newline)
(display (prime? 20)) (newline)
(display (prime? 37)) (newline)
(display (prime? 71)) (newline)
```
실행결과
```
#t
#f
#t
#t
```

<br>

### 시간 측정 기능이 부여된 버전
```scheme
(define (runtime) (get-internal-run-time))

(define (timed-prime-test n)
  (newline)
  (display n)
  (start-prime-test n (runtime)))
(define (start-prime-test n start-time)
  (if (prime? n)
      (report-prime (- (runtime) start-time))))
(define (report-prime elapsed-time)
  (display "***")
  (display elapsed-time))

(timed-prime-test 17) (newline)
```
실행결과
```

17***9100
```

<br>

## 1.2. 2가지 버전의 코드 실행 소요시간 비교
1000, 10000, 100000, 1000000 보다 큰 소수 중에서 가장 작은 3가지 소수를 찾고 소수인걸 판별하는 데에 걸린 시간을 표시하는 코드
### 버전 A

```scheme
(define (search-for-primes start-num max-count)
  (define (search-iter n count)
    (cond ((= count 0 ) n)
          ((prime? n) 
           (timed-prime-test n) 
           (search-for-primes (+ n 1) (- count 1)))
          (else (search-for-primes (+ n 1) count))))
  (search-iter start-num max-count))

(search-for-primes 1000 3) (newline)
(search-for-primes 10000 3) (newline)
(search-for-primes 100000 3) (newline)
(search-for-primes 1000000 3) (newline)
```
실행결과
```

1009***23560
1013***21520
1019***22339

10007***65290
10009***63070
10037***68580

100003***213300
100019***157680
100043***165621

1000003***520280
1000033***533949
1000037***391440
```

<br>

### 버전 B
find-divisor 함수가 숫자 2, 3, 4, 5, 6, 7, ... 처럼 모든 숫자를 검사하지 않고 2 이후로는 홀수만 검사하도록 수정한 코드

```scheme
(define (smallest-divisor n)
  (find-divisor n 2))
(define (find-divisor n test-divisor)
  (cond ((> (square test-divisor) n) n)
        ((divides? test-divisor n) test-divisor)
        (else (find-divisor n (next test-divisor)))))
(define (next n)
  (if (= n 2)
      3
      (+ n 2)))

(search-for-primes 1000 3) (newline)
(search-for-primes 10000 3) (newline)
(search-for-primes 100000 3) (newline)
(search-for-primes 1000000 3) (newline)
```
실행결과
```

1009***14840
1013***8920
1019***9449

10007***36010
10009***77790
10037***32340

100003***103660
100019***119930
100043***104349

1000003***308221
1000033***246870
1000037***264950
```
### 버전 A와 B의 비교
버전 B의 코드가 A의 코드에 비해서 계산에 소요되는 시간을 50% ~ 60% 정도로 단축시켰다.
   
버전 A에서 검사하는 숫자에서 2를 제외한 다른 짝수는 모두 검사하지 않으므로 실행 시간이 2배 빨라져야할 것 같지만 실제로는 2배보다 덜 빨라졌다.
   
이는 next라는 새로운 함수의 호출과, if문, = 체크 등 새로운 연산 요소가 생겼기 때문이다.
   
   
### 참고 링크
guile(scheme의 일종)의 시간 관련 함수 설명 : 
<https://www.csc.kth.se/~mdj/guile-ref/guile-ref_89.html>
   
SICP(마법사책) 솔루션 자료 : 
<https://sicp-solutions.net/post/sicp-solution-exercise-1-23/>


