# 1. SICP(마법사책) 공부

## 1.1. Exercise 2.36

list를 담은 list를 인자로 받는 버전의 accumulate를 만드는 문제.
<br>
원소를 이루는 각 list들의 길이는 동일하다고 가정한다.

```scheme
(define (accumulate-n op init seqs)
  (if (null? (car seqs))
      nil
      (cons (accumulate op init (map car seqs))
            (accumulate-n op init (map cdr seqs)))))

(define s (list (list 1 2 3) (list 4 5 6) (list 7 8 9) (list 10 11 12)))
(display s) (newline)

(display (accumulate-n + 0 s)) (newline) ;(22 26 30)
```
실행결과
```
((1 2 3) (4 5 6) (7 8 9) (10 11 12))
(22 26 30)

```

## 1.2. Exercise 2.37

```
(define mat (list (list 1 2 3) (list 4 5 6))
; 1 2 3
; 4 5 6
```

행렬을 위와 같은 방식으로 표현한다고 할 때,
내적, 행렬곱 등의 행렬 연산을 정의해보는 문제.

```scheme
;내적
(define (dot-product v w)
  (accumulate + 0 (map * v w)))

;행렬과 벡터를 곱하기
(define (matrix-*-vector m v)
  (map (lambda (row) (dot-product row v)) m))

;전치행렬
(define (transpose m)
  (accumulate-n cons nil m))

;행렬과 행렬을 곱하기
(define (matrix-*-matrix m n)
  (let ((cols (transpose n)))
    (map (lambda (m-row) (matrix-*-vector cols m-row)) m)))



;test
(define m (list (list 1 2 3) (list 4 5 6) (list 7 8 9)))
(define n (list (list 1 2) (list 2 3) (list 3 4)))
(define v (list 1 0 (- 1)))
(define w (list 3 7 9))

(display (dot-product v w)) (newline)

(display (matrix-*-vector m v)) (newline)

(display (transpose m)) (newline)

(display (matrix-*-matrix m n)) (newline)
```
실행결과
```
-6
(-2 -2 -2)
((1 4 7) (2 5 8) (3 6 9))
((14 20) (32 47) (50 74))

```
