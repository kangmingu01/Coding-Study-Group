# 1. SICP(마법사책) 공부

# 1.1. 챕터 2.2.3

아래의 코드는 정수로 이루어진 tree에서 홀수인 leaf값만 제곱해서 더한 총합을 구하는 함수이다.

```sceheme
(define x (list 1 (list 2 (list 3 4) 5) (list 6 7)))

(define (sum-odd-squares tree)
  (cond ((null? tree) 0)
        ((not (pair? tree))
         (if (odd? tree) (square tree) 0))
        (else (+ (sum-odd-squares (car tree))
                 (sum-odd-squares (cdr tree))))))
(display (sum-odd-squares x)) (newline)
```

실행결과

```
84

```

<br>

위 함수를 enumerate, filter, map, accumulate라는 추상화된 함수로 다시 써볼 수 있다.

```scheme
(define nil '())

;기준에 따라 값을 골라내는 함수
(define (filter predicate sequence)
  (cond ((null? sequence) nil)
        ((predicate (car sequence))
         (cons (car sequence)
               (filter predicate (cdr sequence))))
        (else (filter predicate (cdr sequence)))))
(display (filter odd? (list 1 2 3 4 5))) (newline)

;모든 원소에 차례로 동일한 연산을 적용시키면서 만든 결과를 반환
(define (accumulate op initial sequence)
  (if (null? sequence)
      initial
      (op (car sequence)
          (accumulate op initial (cdr sequence)))))
(display (accumulate + 0 (list 1 2 3 4 5))) (newline) ;모든 원소를 다 더함
(display (accumulate * 1 (list 1 2 3 4 5))) (newline) ;모든 원소를 다 곱합
(display (accumulate cons nil (list 1 2 3 4 5))) (newline) ;모든 원소를 pair로 엮음 (=리스트로 만들기)

;트리의 모든 leaf를 늘어놓은 리스트를 반환
(define (enumerate-tree tree)
  (cond ((null? tree) nil)
        ((not (pair? tree)) (list tree))
        (else (append (enumerate-tree (car tree))
                      (enumerate-tree (cdr tree))))))
(display (enumerate-tree (list 1 (list 2 (list 3 4)) 5))) (newline)

;결과
(define (sum-odd-squares tree)
  (accumulate +
              0
              (map square
                   (filter odd?
                           (enumerate-tree tree)))))
(display (sum-odd-squares x)) (newline)
```

실행결과

```
(1 3 5)
15
120
(1 2 3 4 5)
(1 2 3 4 5)
84

```

<br>

이런 식으로 차례열에 관한 함수를 작성할 때 
<br>
차례열을 다루는 기본적인 함수(공통 인터페이스)들을 정의하고 이를 엮어서 만들면, 
<br>
함수의 구조도 알아보기 쉽고, 다른 여러 함수들을 작성할 때 모듈 방식으로 접근할 수 있다.
<br>
